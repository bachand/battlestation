#! /usr/bin/env zsh

## Homebrew

if is_silicon_mac
then
  eval "$(/opt/homebrew/bin/brew shellenv)"
else
  eval "$(/usr/local/bin/brew shellenv)"
fi

## History

# For more information on history, see https://wiki.gentoo.org/wiki/Zsh/Guide#History and
# https://unix.stackexchange.com/a/273863.

HISTFILE="$HOME/.zsh_history"
HISTSIZE=50000
SAVEHIST=$HISTSIZE
# Delete old recorded entry if new entry is a duplicate.
setopt HIST_IGNORE_ALL_DUPS
# Don't record an entry starting with a space.
setopt HIST_IGNORE_SPACE

## Grep

export GREP_OPTIONS='--color=auto'

## Terminal

# Resources:
# - https://scriptingosx.com/2019/07/moving-to-zsh-06-customizing-the-zsh-prompt/
# - https://arjanvandergaag.nl/blog/customize-zsh-prompt-with-vcs-info.html

### Prompt

PROMPT='$ '

### Title with git branch

autoload -Uz vcs_info
setopt prompt_subst
# Git status is not included since it's not feasible in large repos.
zstyle ':vcs_info:git:*' formats '%a%b'
zstyle ':vcs_info:*' enable git

update_terminal_title() {
  vcs_info
  print -Pn "\e]1;$vcs_info_msg_0_\a"
}
precmd_functions+=(update_terminal_title)

## fzf

export FZF_DEFAULT_OPTS="--height 40% --reverse --border"
export FZF_CTRL_T_OPTS="--preview 'cat {} | head -100' --preview-window right"
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

## NVM

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

## Rbenv

# Set up Rbenv, if it exists.
if which -s rbenv > /dev/null
then
  path=("$HOME/.rbenv/shims" $path)
  eval "$(rbenv init -)"
fi

## Machine-specific configuration

CONFIG_WORK="$HOME/Google Drive/My Drive/dotfiles/airbnb"
CONFIG_HOME="$HOME/Dropbox/dotfiles/home"

if [[ -f "$CONFIG_WORK" ]]; then
  source "$CONFIG_WORK"
elif [[ -f "$CONFIG_HOME" ]]; then
  source "$CONFIG_HOME"
else
  echo "$(tput setaf 1)Can't find a home or work config file$(tput sgr 0)"
fi

## Completions

# I'm not sure this is strictly necessary but I don't think it can hurt.
autoload -Uz compinit && compinit

## Word style

# Treat slashes as word boundaries.
autoload -Uz select-word-style
select-word-style bash

## Aliases

source "$PARENT_DIR/aliases.zsh"

## Functions

#######################################
# Executes the specified command. Alerts the user when the command completes.
# Arguments:
#   Command to execute
# Returns:
#   None
#######################################
notify() {
  local title="Succeeded"
  # Capture the parameter list for later display.
  local message=$@

  if [[ $# -eq 0 ]]; then
    return
  fi

  # Execute the parameters.
  eval $@

  # Get the exit status of the last-run command.
  local retVal=$?
  if [[ $retVal -ne 0 ]]; then
    title="Failed"
  fi

  # Notify the user that the command is done.
  terminal-notifier -title "$title" -message "$message" -sound "default" -ignoreDnD
}
